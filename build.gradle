// See https://wiki.jenkins-ci.org/display/JENKINS/Gradle+JPI+Plugin

buildscript {
    repositories {
        maven {
            name "jenkins"
            delegate.url("http://repo.jenkins-ci.org/releases/")
        }
        // The plugin is currently only available via the Jenkins
        // Maven repository, but has dependencies in Maven Central.
        mavenCentral()
    }
    dependencies {
        classpath 'org.jenkins-ci.tools:gradle-jpi-plugin:0.5.0'
    }
}
apply plugin: 'jpi'
apply plugin: 'scala'


groupId = "org.jenkins-ci.plugins"
version = "0.0.1-SNAPSHOT"
description = "TestFairy plugin"
sourceCompatibility = 1.6
defaultTasks 'clean', 'jpi'
def scalaVersionMajor = '2.10'
def scalaVersion = "$scalaVersionMajor.3"
def akkaVersion  = "2.2.3"
def sprayVersion = "1.2.0"

jenkinsPlugin {
    coreVersion = '1.420'                                               // Version of Jenkins core this plugin depends on.
    displayName = 'Testfairy plugin'                                    // Human-readable name of plugin.
    url = 'http://wiki.jenkins-ci.org/display/JENKINS/Testfairy-plugin' // URL for plugin on Jenkins wiki or elsewhere.
    gitHubUrl = 'https://github.com/dant3/testfairy-plugin'             // Plugin URL on GitHub. Optional.

    // The developers section is optional, and corresponds to the POM developers section.
    developers {
        developer {
            id 'dant3'
            name 'Vyacheslav Blinov'
            email 'blinov.vyacheslav@gmail.com'
        }
    }
}

compileScala {
    scalaCompileOptions.additionalParameters = ['-Yresolve-term-conflict:object']
}

compileTestScala {
    scalaCompileOptions.additionalParameters = ['-Yresolve-term-conflict:object']
}

repositories {
    mavenCentral()
}

dependencies {
    // java dependencies
    compile 'org.apache.httpcomponents:httpclient:4.3.3'
    compile 'org.apache.httpcomponents:httpmime:4.3.3'
    compile 'org.apache.commons:commons-lang3:3.3.2'
    compile 'org.apache.commons:commons-io:1.3.2'

    // scala dependencies
    compile "org.scala-lang:scala-library:$scalaVersion"
    compile "org.scala-lang:scala-compiler:$scalaVersion"
    compile "io.spray:spray-client:$sprayVersion"
    compile "io.spray:spray-httpx:$sprayVersion"
    compile "io.spray:spray-json_$scalaVersionMajor:1.2.6"

    compile "net.databinder:dispatch-mime_$scalaVersionMajor:0.8.10"
    compile "net.databinder:dispatch-core_$scalaVersionMajor:0.8.10"

    compile "com.typesafe.akka:akka-actor_$scalaVersionMajor:$akkaVersion"


    testCompile "com.typesafe.akka:akka-testkit_$scalaVersionMajor:$akkaVersion"
    testCompile "org.specs2:specs2-core_$scalaVersionMajor:2.3.7"
    testCompile "io.spray:spray-testkit:" + sprayVersion

    testCompile "org.mock-server:mockserver-netty:2.10"
}

//tasks.withType(ScalaCompile) {
//    scalaCompileOptions.useAnt = false
//}